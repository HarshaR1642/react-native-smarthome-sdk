buildscript {
  ext.getExtOrDefault = { name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['SmarthomeSdk_' + name]
  }

  repositories {
    google()
    mavenCentral()
    maven {
      setUrl("https://jitpack.io")
      content {
        includeGroup("com.github.aasitnikov")
      }
    }
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.7.3"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
    classpath 'com.github.aasitnikov:fat-aar-android:1.4.1'
  }
}


def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: 'com.kezong.fat-aar'

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["SmarthomeSdk_" + name]).toInteger()
}

def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  return (major == 7 && minor >= 3) || major >= 8
}

android {
  if (supportsNamespace()) {
    namespace "com.smarthomesdk"

    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifestNew.xml"
      }
//
//      getByName("main") {
//        jniLibs.srcDirs("libs")
//      }
    }
  }

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  kotlinOptions {
    jvmTarget = "17"
  }

  buildFeatures {
    buildConfig true
  }

  publishing {
    singleVariant("release") {
      withJavadocJar()
      withSourcesJar()
    }
  }
}

repositories {
  mavenCentral()
  google()
}

dependencies {
  implementation "com.facebook.react:react-android:0.74.4"

  embed(project(path: ":lottie-react-native", configuration: 'default'))
  embed(project(path: ":react-native-async-storage", configuration: 'default'))
  embed(project(path: ":react-native-picker", configuration: 'default'))
  embed(project(path: ":react-native-screens", configuration: 'default'))
  embed(project(path: ":react-native-clipboard", configuration: 'default'))
  embed(project(path: ":react-native-gesture-handler", configuration: 'default'))
  embed(project(path: ":react-native-safe-area-context", configuration: 'default'))
  embed(project(path: ":react-native-svg", configuration: 'default'))
  embed(project(path: ":react-native-linear-gradient", configuration: 'default'))
  embed(project(path: ":react-native-reanimated", configuration: 'default'))

  if (project.properties["hermesEnabled"] as Boolean) {
    implementation("com.facebook.react:hermes-android")
  } else {
    implementation("org.webkit:android-jsc:+")
  }
}

apply from: "${projectDir}/publish.gradle"
